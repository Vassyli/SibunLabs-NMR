# 1 "/u/exp/stan/nmr/lists/pp/invietgpsi"
;invietgpsi
;avance-version
;2D H-1/X correlation via double inept transfer
;   using sensitivity improvement
;phase sensitive using Echo/Antiecho-TPPI gradient selection
;with decoupling during acquisition
;using trim pulses in inept transfer
;A.G. Palmer III, J. Cavanagh, P.E. Wright & M. Rance, J. Magn.
;   Reson. 93, 151-170 (1991)
;L.E. Kay, P. Keifer & T. Saarinen, J. Am. Chem. Soc. 114,
;   10663-5 (1992)
;J. Schleucher, M. Schwendinger, M. Sattler, P. Schmidt, O. Schedletzky,
;   S.J. Glaser, O.W. Sorensen & C. Griesinger, J. Biomol. NMR 4,
;   301-306 (1994)




# 1 "/u/exp/stan/nmr/lists/pp//Avance.incl"
;Avance.incl
;
;version  99/08/02





























# 19 "/u/exp/stan/nmr/lists/pp/invietgpsi"

# 1 "/u/exp/stan/nmr/lists/pp//Grad.incl"
;Grad.incl  -  include file for Gradient Spectroscopy
;avance-version
;version 99/02/19
















define list<gradient> EA=<EA>
# 20 "/u/exp/stan/nmr/lists/pp/invietgpsi"

# 1 "/u/exp/stan/nmr/lists/pp//Delay.incl"
;Delay.incl  -  include file for commonly used delays
;avance-version
;version 99/02/19


;general delays

define delay DELTA
define delay DELTA1
define delay DELTA2
define delay DELTA3
define delay DELTA4
define delay DELTA5
define delay DELTA6
define delay DELTA7
define delay DELTA8

define delay TAU
define delay TAU1
define delay TAU2
define delay TAU3
define delay TAU4
define delay TAU5


;delays for centering pulses

define delay CEN_HN1
define delay CEN_HN2
define delay CEN_HN3
define delay CEN_HC1
define delay CEN_HC2
define delay CEN_HC3
define delay CEN_HC4
define delay CEN_CN1
define delay CEN_CN2


;loop counters

define loopcounter COUNTER
define loopcounter SCALEF
define loopcounter FACTOR1
define loopcounter FACTOR2
define loopcounter FACTOR3
# 21 "/u/exp/stan/nmr/lists/pp/invietgpsi"



"p2=p1*2"

"p4=p3*2"

"d0=3u"

"d4=1s/(cnst2*4)"

"d11=30m"

"d13=3u"


"DELTA=p16+d16+50u+p2+d0*2"
"DELTA1=d13+p16+d16+4u"

"l3=(td1/2)"


1 ze
  d11 pl12:f2
2 d1 do:f2 
  6m
3 d11
  18m
4 (p1 ph1)
  d4 pl2:f2
  (p2 ph1) (p4 ph6):f2
  d4
  p28 ph1
  d13
  (p1 ph2) (p3 ph3):f2
  d0 
  p2 ph7
  d0
  50u setnmr2|0 setnmr0|34
  p16:gp1*EA
  d16
  (p4 ph4):f2
  DELTA
  (p1 ph1) (p3 ph4):f2
  d24
  (p2 ph1) (p4 ph1):f2
  d24
  (p1 ph2) (p3 ph5):f2
  d4
  (p2 ph1) (p4 ph1):f2
  d4
  (p1 ph1)
  DELTA1
  (p2 ph1)
  d13
  p16:gp2
  d16 pl12:f2
  4u  setnmr2^0 setnmr0^34
  go=2 ph31 cpd2:f2
  d1 do:f2 wr #0 if #0 zd
  3m ip5 igrad EA
  3m ip5
  lo to 3 times 2
  d11 id0
  3m ip3
  3m ip3
  3m ip6
  3m ip6
  3m ip31
  3m ip31
  lo to 4 times l3
exit
   

ph1=0 
ph2=1
ph3=0 2
ph4=0 0 2 2
ph5=1 1 3 3
ph6=0
ph7=0 0 2 2
ph31=0 2 2 0
  

;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl12: f2 channel - power level for CPD/BB decoupling
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p4 : f2 channel - 180 degree high power pulse
;p16: homospoil/gradient pulse
;p28: f1 channel - trim pulse
;d0 : incremented delay (2D)                  [3 usec]
;d1 : relaxation delay; 1-5 * T1
;d4 : 1/(4J)XH
;d11: delay for disk I/O                             [30 msec]
;d13: short delay                                    [3 usec]
;d16: delay for homospoil/gradient recovery
;d24: 1/(4J)XH for XH
;     1/(8J)XH for all multiplicities
;cnst2: = J(XH)
;l3: loop for phase sensitive 2D using  E/A method : l3 = td1/2
;in0: 1/(2 * SW(X)) = DW(X)
;nd0: 2
;NS: 1 * n
;DS: >= 16
;td1: number of experiments
;MC2: echo-antiecho
;cpd2: decoupling according to sequence defined by cpdprg2
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:	gp 1 : gp 2
;			  80 : 20.1    for C-13
;			  80 :  8.1    for N-15

;for z-only gradients:
;gpz1: 80%
;gpz2: 20.1% for C-13, 8.1% for N-15

;use gradient files:   
;gpnam1: SINE.100
;gpnam2: SINE.100
